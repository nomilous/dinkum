#!/usr/bin/env coffee

{Client} = require '../lib/dinkum'
pipeline = require 'when/pipeline'
parallel = require 'when/parallel'

interval = undefined

client1 = Client.create  

    allowUncertified: true
    hostname: 'localhost'
    port: 3000

client2 = Client.create
    
    #
    # * limit 100 requests on the queue
    # * process 4 at a time
    #

    queueLimit: 100
    requestLimit: 4
    allowUncertified: true
    hostname: 'localhost'
    port: 3000


pipeline( [

    (      ) -> client1.get path: '/get/100/records'
    (result) -> 

        #console.log "#{result.body[0..50]}..."
        data = JSON.parse result.body
        
        parallel( for record in data.records

            do (record) -> 

                # -> client2.post path: '/update', json: record
                -> client2.get path: '/post/update/' + record.id
            
        )


] ).then( 
    
    (result) -> 
        console.log result.map( (r) -> r.statusCode ).join ' '
        clearInterval interval

    (error) -> 
        console.log error.stack
        clearInterval interval

)


interval = setInterval (->

    client2.stats().then (stats) -> 

        console.log "pending: #{stats.pending.count}, active: #{stats.active.count}, done: #{stats.done.count} "

), 1000
